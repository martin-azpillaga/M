grammar m.JSON hidden(WHITE_SPACE)

import "http://www.eclipse.org/emf/2002/Ecore"

generate json "http://www.minim.games/json"

Object: {Object}
	'{' (fields+=Field (',' fields+=Field)*)? '}'
;

Field: name=STRING ':' value=Value;

Value: Object|Array|StringValue|FloatValue|LongValue|BooleanValue;

Array: {Array} '[' (elements+=Value (',' elements+=Value)*)? ']';

StringValue: value=STRING;
FloatValue: value=FLOAT;
LongValue: value=LONG;
BooleanValue: value=Boolean;

enum Boolean: TRUE='true'|FALSE='false';
terminal LONG: '-'? '0'..'9'+;
terminal FLOAT: '-'? ('0'..'9')* '.' ('0'..'9')+ ('e' '-'? '0'..'9'+)?;
terminal STRING: '"' ( !('"') )* '"';
terminal WHITE_SPACE: (' '|'\t'|'\r'|'\n')+;
terminal ID: ('0'..'9'|'a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'.'|'-')*;