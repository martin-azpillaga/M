grammar m.M with m.Modular

import "http://www.minim.games/modular"
import "http://www.eclipse.org/emf/2002/Ecore"
generate m "http://www.minim.games/M"

Modul:
	entities+=Entity*;

Entity:
	name=FILENAME 
	(':' base=FILENAME)?
	'{'
	components+=Component (',' components+=Component)*
	'}';

Component:
	name=IDENTIFIER
	value=(Vector | Asset)?;

Vector:
	values+=NUMBER+;

Asset:
	value=FILENAME;

@Override
Procedure:
	name=FILENAME
	'{'
	statements+=Statement+
	('←' return=Expression)?
	'}';

@Override
Statement:
	Selection | Iteration | Call | Assignment | Loop;

Loop:
	'∀' identifier=IdentifierLiteral ('∈' tags+=IdentifierLiteral*)?
	'{' statements+=Statement* '}';

ACCESS:
	IDENTIFIER ('.' IDENTIFIER)*;

@Override
IdentifierLiteral:
	value=ACCESS;

@Override
Assignment:
	identifier=ACCESS kind=AssignmentKind expression=Expression;

@Override
Primary returns Expression:
	Variable |
	'(' Expression ')';