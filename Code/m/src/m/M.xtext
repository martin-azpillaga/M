grammar m.M with m.Modular
import "azpillaga.world/modular"
generate m "azpillaga.world/m"

Game:
	(archetypes+=Archetype|systems+=System)*
	end=End;

Archetype:
	 name = WORD '≔'
	(base=WORD '∪')?
	 '{' components+=Component (','? components+=Component)* '}';

Component:
	name=WORD|
	{VectorComponent} name=WORD '[' entries+=NUMBER (','? entries+=NUMBER)* ']';

System:
	name=WORD '≔'
	statements+=Statement+;

@Override
Statement:
	Selection|Iteration|Forall|Exists|Assignment|Function;

Forall returns Block: {Forall}
	 '∀' variable=WORD
	('∈' collection=Expression )?
	('|' condition=Expression)?
	 '[' statements+=Statement (','? statements+=Statement)* ']';

Exists returns Block: {Exists}
	(negated?='∄'|'∃')
	 variable=WORD
	('∈' collection=Expression )?
	('|' condition=Expression)?
	 '[' statements+=Statement (','? statements+=Statement)* ']';

@Override
Branch returns Block: {Branch}
	condition=Expression
	'[' statements+=Statement (','? statements+=Statement)* ']';

@Override
Block returns Block: {Block}
	'[' statements+=Statement (','? statements+=Statement)* ']';

@Override
Iteration returns Block: {Iteration}
	'↺' condition=Expression
	'[' statements+=Statement (','? statements+=Statement)* ']';

@Override
LogicalNot returns Expression: {LogicalNot} 
	'¬' expression=Primary;

@Override
Equality returns Expression:
	Comparison ({Equality.left=current}  kind=EqualityKind right=Comparison)*;

Assignment:
	variable=(Variable|ComponentAccess) '=' expression=Expression
;

ComponentAccess returns Expression: {ComponentAccess}
	entity=WORD '.' component=WORD;

@Override
Primary returns Expression:
	=>ComponentAccess|=>Function|Variable|Set|LogicalNot|Brackets
;

@Override
Function returns Expression: {Function}
	name=WORD
	'[' (arguments+=Expression (','? arguments+=Expression)*)? ']'
;

Set returns Expression: {Set}
	'{' variable=WORD ('∈' superset=Expression)? '|' predicate=Expression '}'
;

End: {End};

@Override
enum EqualityKind: equal='='|notEqual='≠';
@Override
enum MultiplicativeKind: multiply='×'|divide='÷';
enum AmountKind: lower='<'|lowerOrEqual='≤'|equal='='|notEqual='≠'|greaterOrEqual='≥'|greater='>';