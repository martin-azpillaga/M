grammar m.M with m.Modular

import "http://www.minim.games/modular"
import "http://www.eclipse.org/emf/2002/Ecore"
generate m "http://www.minim.games/M"

Modul:
	(entities+=Entity|systems+=System)*;

Entity:
	name=FILENAME 
	(':' base=FILENAME)?
	'{'
	components+=Component (',' components+=Component)*
	'}';

Component:
	name=IDENTIFIER
	value=(Vector | Asset)?;

Vector:
	entries+=NUMBER+;

Asset:
	name=FILENAME;

System returns Procedure:
	name=FILENAME
	'{'
	statements+=Statement+
	('←' returnValue=Expression)?
	'}';

@Override
Statement:
	Selection | Iteration | FunctionCall | Assignment | Loop;

Loop:
	'∀' entity=IDENTIFIER 
	('∈' tags+=IDENTIFIER*)?
	'{' statements+=Statement* '}';

ACCESS:
	IDENTIFIER ('.' IDENTIFIER)*;

@Override
IdentifierLiteral:
	value=ACCESS;

@Override
Assignment:
	variable=ACCESS
	kind=AssignmentKind
	expression=Expression;

@Override
Primary returns Expression:
	Variable | '(' Expression ')';