grammar m.CSharp with m.Modular hidden(WS, MULTIPLE_LINES_COMMENT, SINGLE_LINE_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore"
import "http://www.minim.games/modular"
generate csharp "http://www.minim.games/csharp"

Model:
	usings+=Using*
	types+=Type*;

Using:
	'using' name=QualifiedName ';';

Type:
	{Type}
	annotations+=Annotation*
	visibility=Visibility?
	(({Struct.info=current} 'struct' name=TYPENAME
	|
	{Class.info=current} sealed?='sealed'? static?='static'? 'class' name=TYPENAME)
	(':' superTypes+=TYPENAME (',' superTypes+=TYPENAME)*)?
	'{' members+=Member* '}'
	|
	{Enum.info=current}
	'enum' name=TYPENAME
	'{' (values+=IDENTIFIER (',' values+=IDENTIFIER)*)? '}');

Info:
	Member | Type;

Member:
	{Member}
	annotations+=Annotation*
	visibility=Visibility?
	(({Struct.info=current} 'struct' name=TYPENAME
	|
	{Class.info=current} static?='static'? 'class' name=TYPENAME)
	(':' superTypes+=TYPENAME (',' superTypes+=TYPENAME)*)?
	'{' members+=Member* '}'
	|
	{Enum.info=current}
	'enum' name=TYPENAME
	'{' (value+=IDENTIFIER (',' values+=IDENTIFIER)*)? '}'
	|
	(({Method.info=current} override?='override')?
	({Method.info=current} static?='static')?
	type=TYPENAME
	({Field.info=current} name=IDENTIFIER ';'
	|
	{Method.info=current} name=TYPENAME '(' (arguments+=Argument (',' arguments+=Argument)*)? ')'
	'{'
	statements+=Statement*
	'}')));

enum Visibility:
	public | protected | private;

@Override
Expression:
	Ternary ({InlineAssignment.left=current} kind=AssignmentKind right=Ternary)*;

@Override
UnaryMinus returns Expression:
	UnaryPlus | {UnaryMinus} '-' expression=UnaryPlus;

UnaryPlus returns Expression:
	PreDecrement | {UnaryPlus} '+' expression=PreDecrement;

PreDecrement returns Expression:
	PreIncrement | {PreDecrement} '--' expression=PreIncrement;

PreIncrement returns Expression:
	Primary | {PreIncrement} '++' expression=Primary;

@Override
Statement:
	Selection | Iteration | FunctionCall | Assignment | For | Declaration | Return;

@Override
Iteration:
	'while' '(' condition=Expression ')'
	'{' statements+=Statement* '}';

@Override
Selection:
	'if' branches+=Branch
	('else' 'if' branches+=Branch)*
	('else' '{' statements+=Statement* '}')?;

@Override
Branch:
	'(' condition=Expression ')'
	'{' statements+=Statement* '}';

For:
	'for' '(' initialization=Assignment ';' condition=Expression ';' increment=Assignment ')'
	'{'
	statements+=Statement*
	'}';

Return:
	'return' expression=Expression ';';

Declaration:
	(var?='var' | type=TYPENAME) access=ACCESS 
	(assignmentType=AssignmentKind expression=Expression)? ';';
	
@Override
Assignment:
	variable=ACCESS kind=AssignmentKind expression=Expression ';'
;

@Override
FunctionCall:
	name=IDENTIFIER
	'(' (parameters+=Expression (',' parameters+=Expression)*)? ')' ';';

@Override
IdentifierLiteral:
	value=ACCESS;

Annotation:
	'[' call=FunctionCall ']';

Argument:
	annotations+=Annotation* ref?='ref'? type=TYPENAME name=IDENTIFIER;

Lambda: {Lambda}
	'(' (arguments+=Argument (',' arguments+=Argument)*)? ')' '=>' '{' statements+=Statement* '}';

Index: identifier=IDENTIFIER  '[' (parameters+=Expression ( ',' parameters+=Expression)*)?  ']';

@Override
Variable: IdentifierLiteral|FunctionCall|Index;

ACCESS:
	IDENTIFIER ('.' IDENTIFIER)*;

QualifiedName:
	IDENTIFIER ('.' IDENTIFIER)*;

TYPENAME:
	IDENTIFIER ('<' TYPENAME (',' TYPENAME)* '>')?;

terminal MULTIPLE_LINES_COMMENT:
	'/*'->'*/';

terminal SINGLE_LINE_COMMENT:
	'//' !('\n' | '\r')* ('\r'? '\n')?;
 