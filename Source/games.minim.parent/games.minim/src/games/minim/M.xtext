grammar games.minim.M with org.eclipse.xtext.common.Terminals

generate m "http://www.minim.games/M"

Game:
	(entities+=Entity|systems+=System)*
	(MAIN_ENTITIES mainEntities+=[Entity]+ '.')?
	functions+=Function*
	subrutines+=Subrutine*
	(AND
		title = ID '.'
	tagComponents+=Name+ '.'
	real1Components+=Name+ '.'
	real2Components+=Name+ '.'
	real3Components+=Name+ '.'
	real4Components+=Name+ '.'
	enumerationComponents+=Name '.'
	referenceComponents+=Name '.'
	timerComponents+=Name '.'
	triggerComponents+=Name '.'
	rangeComponents+=Name '.'
	vectorComponents+=Name '.'
	sensorComponents+=Name '.'
	textComponents+=Name '.'
	entityComponents+=Name '.'
	spriteComponents+=Name '.'
	audioComponents+=Name '.'
	meshComponents+=Name '.'
	materialComponents+=Name '.'
	fontComponents+=Name '.'
	
	sprites+=Name '.'
	audios+=Name '.'
	meshes+=Name '.'
	materials+=Name '.'
	texts+=Name '.'
	fonts+=Name '.'
	enumerations+=Enum '.'
	
	transformations+=Name '.'
	voids+=Name '.')?
;
Expression: ArithmeticExpression | BooleanExpression;
System returns Control:	name=ID START_SYSTEM commands+=Command+;

Command: Control|Assignment|SubrutineCall|Break|Initialization;

Break: {Break} STOP_ITERATION;
Initialization: DECLARE_VARIABLE variable=Name;

Constraint:
	negated?=NEGATE_CONSTRAINT? event=EventType? component=[Name]
;


Control:
	  {Loop} FOR (all?=ALL|number=ArithmeticExpression) ENTITIES group=Name (CONSTRAINTS constraints+=Constraint+)?
	  		entities?='entities'?
	  		('data' datas+=[Name]+)?
	  		'{' commands+=Command+ '}'
	| {Check} IF condition=Condition
			( ELSEIF conditions+=Condition )*
			( ELSE '{' elseCommands+=Command+ '}' )?
	| {While} WHILE condition=BooleanExpression '{' commands+=Command+ '}'	
;

Condition returns Control:
	{Condition} condition=BooleanExpression '{' commands+=Command+ '}'
;

Assignment:
	  {ComponentAssignment} group=[Name] '.' component=[Name] assignment=AssignmentType expression=ArithmeticExpression
	| {VariableAssignment} variable=Name assignment=AssignmentType expression=ArithmeticExpression
;

SubrutineCall:
	subrutine=[Name] '('  parameters+=ArithmeticExpression (',' parameters+=ArithmeticExpression)* ')'
;

Atomic returns ArithmeticExpression: 
	   ({Access} group=[Name] '.' component=[Name] (range?=TILT|vector?=VECTOR)?)
	|  ({Call} function=[Name] '(' parameters+=ArithmeticExpression (',' parameters+=ArithmeticExpression)* ')')
	|  ({Pop} variable=[Name])
;

Function:
	'function' name=ID '(' parameters+=Name (',' parameters+=Name)* ')' '{' commands+=Command+ 'return' '}'
;
Subrutine:
	'subrutine' name=ID '(' parameters+=Name (',' parameters+=Name)* ')' '{' commands+=Command+ '}'
;

Entity:
	name=ID 
	(BASE_ENTITY base=[Entity] (REMOVED_COMPONENTS removed+=[Name] (',' removed+=[Name])*)?)?
	(ENTITY_COMPONENTS	values+=Value (','? values+=Value)*)? 
	(ENTITY_CHILDREN children+=Entity+)? '.';
	
Value:
	  {Tag}   component=[Name]
	| {Real1} component=[Name] x=Number
	| {Real2} component=[Name] x=Number y=Number
	| {Real3} component=[Name] x=Number y=Number z=Number
	| {Real4} component=[Name] x=Number y=Number z=Number w=Number
	| {Enumeration} component=[Name] '['values+=[Name]+']'
	| {Reference} component=[Name] REFERENCE entity=[Entity]
	| {Sprite} component=[Name] SPRITE value=[Name]
	| {Audio} component=[Name] AUDIO value=[Name]
	| {Text} component=[Name] value=STRING
	| {Material} component=[Name] MATERIAL value=[Name]
	| {Mesh} component=[Name] MESH value=[Name]
	| {Font} component=[Name] FONT value=[Name]
	| {Timer} component=[Name] value=Number SECONDS
	| {Trigger} component=[Name] value=TriggerType
	| {Range} component=[Name] value=AxisType
	| {Vector} component=[Name] value=VectorType
	| {Sensor} component=[Name] SENSOR '['values+=[Name]+']' (RAY rayX=Number rayY=Number )?
;

BooleanExpression:
  Boolean1 ({Or.left=current} OR right=Boolean1)*;
Boolean1 returns BooleanExpression:
  PrimaryB ({And.left=current} AND right=PrimaryB)*;
PrimaryB returns BooleanExpression:
    =>AtomicB 
  | {BooleanBrackets} '(' expression=BooleanExpression ')' 
  | {Not} NOT expression=PrimaryB;
AtomicB returns BooleanExpression:
	  {Comparison} left=Addition type=RelationType right=Addition
	| {BooleanVariable} variable=[Name]
;
ArithmeticExpression: Addition;
Addition returns ArithmeticExpression:
  Multiplication (({Plus.left=current} '+'|{Minus.left=current} '-') right=Multiplication)*;
Multiplication returns ArithmeticExpression:
  Exponentiation (({Times.left=current} '*'|{Divide.left=current}'/'|{Modulus.left=current}'%') right=Exponentiation)*;
Exponentiation returns ArithmeticExpression:
	Bitwise ({Exponentiation.left=current} '**' right=Bitwise)*;
Bitwise returns ArithmeticExpression:
	Unary ({Bitwise.left=current} op=('&'|'|'|'~') right=Unary)*;
Unary returns ArithmeticExpression:
	Primary ({Increment.left=current} '++'|{Decrement.left=current}'--')?;
Primary returns ArithmeticExpression:
  Atomic |
  {ArithmeticBrackets}'(' expression=Addition ')';



Enum: name=ID values+=Name+;
Name: name=ID;
Number:	{Decimal} negative?='-'? wholePart=INT ('.' decimalPart=INT)?;

terminal REFERENCE: 'entity.';
terminal SPRITE: 'sprite.';
terminal AUDIO: 'audio.';
terminal SENSOR: 'detect';
terminal MESH: 'mesh.';
terminal MATERIAL: 'material.';
terminal FONT: 'font.';
terminal TILT: 'tilt';
terminal VECTOR: 'vector';
terminal OR: 'or';
terminal AND: 'and';
terminal NOT: '!';
terminal STOP_ITERATION: 'break';
terminal DECLARE_VARIABLE: 'initialize';
terminal BASE_ENTITY: 'based on';
terminal ENTITY_COMPONENTS: 'has';
terminal REMOVED_COMPONENTS: 'without';
terminal ENTITY_CHILDREN: 'contains';
terminal MAIN_ENTITIES: 'main world contains';
terminal START_SYSTEM: ':';
terminal FOR: 'for';
terminal ALL: 'all';
terminal ENTITIES: 'entity';
terminal CONSTRAINTS: 'with';
terminal NEGATE_CONSTRAINT: 'no';
terminal IF: 'if';
terminal ELSEIF: 'else if';
terminal ELSE: 'else';
terminal WHILE: 'while';
terminal SECONDS: 's';
terminal RAY: 'ray';

enum EventType: tag|timeout='timed out'|trigger='triggered'|enter='enter'|stay='stay'|exit='exit';
enum AxisType: 
	LeftX='gamepad.leftX'|LeftY='gamepad.leftY'|RightX='gamepad.rightX'|RightY='gamepad.rightY'
	|LeftShoulder='gamepad.leftShoulder'|RightShoulder='gamepad.rightShoulder'
;
enum TriggerType: 
	South='gamepad.south'|West='gamepad.west'|East='gamepad.east'|North='gamepad.north'
	|LeftTrigger='gamepad.leftTrigger'|RightTrigger='gamepad.rightTrigger'
	|Down='gamepad.down'|Left='gamepad.left'|Right='gamepad.right'|Up='gamepad.up'
	|Start='gamepad.start'|Select='gamepad.select'
	|LeftStick='gamepad.leftStickPress'|RightStick='gamepad.rightStickPress'
	|Optional='gamepad.optional'
;
enum VectorType: mouse='mouse.position'|dpad='gamepad.dpad'|leftStick='gamepad.leftStick'|rightStick='gamepad.rightStick';
enum RelationType: over='>'|under='<'|equal='='|notequal='!='|overorequal='>='|underorequal='<=';
enum AssignmentType: set='='|increase='+='|decrease='-='|multiply='*='|divide='/='|modulus='%='|and='&='|or='|=';
enum UnityComponentType returns EngineComponentType:
	canvas='nonono'|bodyType|serializedVersion
	|orthographic|orthographicSize|nearClip|clearFlags
	|lightType|screenExpand|screenSize|screenPosition
	|inputModule|graphicRaycaster|eventSystem|uiScaleMode|font|canvasRenderer
;
enum EngineComponentType: 
	ears|perspective|trigger|mass|position|rotation|scale
	|radius|velocity|acceleration|emission
	|intensity|range|spotAngle|viewAngle|viewDistance
	|extent|force|restitution|friction
	|volume|pitch|dopplerEffect
	|clearColor|canvasScaler='canvas'
	|sprite='appearance'|mesh|material|audio='audiosource'|text|number;
enum EngineTransformationType: abs|sqrt|cos|sin|tan|exp|log|random|create|x|y|z|w|join;
enum EngineVoidType: create|destroy|add|remove;
enum FieldType: value|x|y|z|w;